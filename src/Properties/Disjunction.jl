"""
    Disjunction <: Property

Type that represents a disjunction of properties.

### Fields

- `disjuncts` -- vector of properties (elements are reordered by this type)
- `reorder`   -- flag to indicate whether shuffling is allowed

### Notes

The following formula characterizes whether a set ``X`` satisfies a disjunction
``ğ‘ƒ = ğ‘ƒ_1 âˆ¨ ğ‘ƒ_2 âˆ¨ â€¦ âˆ¨ ğ‘ƒ_m``:

```math
    X \\models ğ‘ƒ \\iff X \\models ğ‘ƒ_j \\text{ for some } 1 â‰¤ j â‰¤ m
```

If the `reorder` flag is set, the disjuncts may be reordered after each call to
[`check(ğ‘ƒ::Disjunction, X::LazySet)`](@ref) as a heuristics to make subsequent
checks faster.
"""
struct Disjunction <: Property
    disjuncts::Vector{Property}
    reorder::Bool
end

# default constructor with activated reordering
Disjunction(disjuncts::Vector{<:Property}) = Disjunction(disjuncts, true)

"""
    check(ğ‘ƒ::Disjunction, X::LazySet)::Bool

Check whether a convex set satisfies a disjunction of properties.

### Input

- `ğ‘ƒ` -- disjunction of properties
- `X` -- convex set

### Output

`true` iff `X` satisfies the disjunction of properties `ğ‘ƒ`.

### Notes

If the `ğ‘ƒ.reorder` flag is set, the disjuncts may be reordered as a heuristics
to make subsequent checks faster.
Since we check satisfaction from left to right, we move the disjunct for which
satisfaction was established to the front.
"""
function check(ğ‘ƒ::Disjunction, X::LazySet)::Bool
    for (i, conjunct) in enumerate(ğ‘ƒ.disjuncts)
        if check(conjunct, X)
            _reorder!(ğ‘ƒ, i)
            return true
        end
    end
    return false
end

function _reorder!(ğ‘ƒ::Disjunction, i::Int)
    if !ğ‘ƒ.reorder || i == 1
        return nothing
    end
    first = ğ‘ƒ.disjuncts[i]
    while i > 1
        ğ‘ƒ.disjuncts[i] = ğ‘ƒ.disjuncts[i-1]
        i -= 1
    end
    ğ‘ƒ.disjuncts[1] = first
    return nothing
end
