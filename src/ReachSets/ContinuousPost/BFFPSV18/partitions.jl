"""
    inout_map_property(ğ‘ƒ::PROPERTY,
                       partition::AbstractVector{<:AbstractVector{Int}},
                       blocks::AbstractVector{Int},
                       n::Int
                      )::PROPERTY where {PROPERTY<:Property}

Map a property to the dimensions of analyzed blocks.

### Input

- `ğ‘ƒ`         -- property
- `partition` -- block partition; elements are start and end indices of a block
- `blocks`    -- list of all output block indices in the partition
- `n`         -- total number of input dimensions

### Output

A new property of reduced dimension.

### Notes

If the dimension is not reduced, we return the original property.
Otherwise, the dimension reduction is implemented via a (lazy) `LinearMap`.
"""
function inout_map_property(ğ‘ƒ::PROPERTY,
                            partition::AbstractVector{<:AbstractVector{Int}},
                            blocks::AbstractVector{Int},
                            n::Int
                           )::PROPERTY where {PROPERTY<:Property}
    proj = projection_map(partition, blocks)
    if length(proj) == n
        # no change in the dimension, return the original property
        return ğ‘ƒ
    elseif length(proj) == dim(ğ‘ƒ)
        # temporary workaround, should be fixed properly by #521
        return ğ‘ƒ
    else
        M = sparse(proj, proj, ones(length(proj)), n, n)
        return inout_map_property_helper(ğ‘ƒ, M)
    end
end

function inout_map_property_helper(ğ‘ƒ::Conjunction, M::AbstractMatrix)
    new_conjuncts = similar(ğ‘ƒ.conjuncts)
    for (i, conjunct) in enumerate(ğ‘ƒ.conjuncts)
        new_conjuncts[i] = inout_map_property_helper(conjunct, M)
    end
    return Conjunction(new_conjuncts)
end

function inout_map_property_helper(ğ‘ƒ::Disjunction, M::AbstractMatrix)
    new_disjuncts = similar(ğ‘ƒ.disjuncts)
    for (i, disjunct) in enumerate(ğ‘ƒ.disjuncts)
        new_disjuncts[i] = inout_map_property_helper(disjunct, M)
    end
    return Disjunction(new_disjuncts)
end

function inout_map_property_helper(ğ‘ƒ::BadStatesProperty, M::AbstractMatrix)
    @assert dim(ğ‘ƒ.bad) == size(M, 2) "the property has dimension " *
        "$(dim(ğ‘ƒ.bad)) but should have dimension $(size(M, 2))"
    return BadStatesProperty(M * ğ‘ƒ.bad)
end

function inout_map_property_helper(ğ‘ƒ::SafeStatesProperty, M::AbstractMatrix)
    @assert dim(ğ‘ƒ.safe) == size(M, 2) "the property has dimension " *
        "$(dim(ğ‘ƒ.safe)) but should have dimension $(size(M, 2))"
    return SafeStatesProperty(M * ğ‘ƒ.safe)
end

"""
    projection_map(partition::AbstractVector{<:AbstractVector{Int}},
                   blocks::AbstractVector{Int}
                  )::Vector{Int}

Create a sorted list of all dimensions in `blocks`.

### Input

- `partition` -- block partition; elements are start and end indices of a block
- `blocks`    -- list of all output block indices in the partition

### Output

A sorted list containing all output dimensions.
"""
function projection_map(partition::AbstractVector{<:AbstractVector{Int}},
                        blocks::AbstractVector{Int}
                       )::Vector{Int}
    proj = Vector{Int}()
    for bi in blocks
        for i in partition[bi]
            push!(proj, i)
        end
    end
    return proj
end
